      SUBROUTINE SRZERO (INPUT, OUTPUT)
      OPEN "BP.UPL" TO BP.UPL ELSE
         OUTPUT = "900-Cannot open BP.UPL"
         RETURN
      END
      READ CONTROL FROM BP.UPL, "LOADER" THEN LOADING=1 ELSE LOADING=0; CONTROL=""
      true  = 1
      false = 0
      OUTPUT = ""
      INPUT  = TRIM(INPUT)
      ZERROR  = false
      * ---------------------------------------------------
***REALITY
***   WORDS = CONVERT(INPUT, " ", @VM)
      WORDS = CONVERT(" ", @VM, INPUT)
      EVENT = WORDS<1,1>
      print INPUT
      BEGIN CASE
         CASE LOADING
            GOSUB LOAD..DATA
         CASE EVENT = "LOAD"
            GOSUB PREPARE..LOADER
         CASE 1
            GOSUB EXECUTE..CMD
      END CASE
      * ---------------------------------------------------
      RETURN
      *
      ****************** [ EXECUTE..CMD ] *****************
      *
EXECUTE..CMD:
      EXECUTE INPUT CAPTURING OUTPUT
***REALITY
***   OUTPUT = CONVERT(OUTPUT, @FM, " ")
***   OUTPUT = CONVERT(OUTPUT, @VM, " ")
***   OUTPUT = CONVERT(OUTPUT, @SM, " ")
      OUTPUT = CONVERT(@FM, " ", OUTPUT)
      OUTPUT = CONVERT(@VM, " ", OUTPUT)
      OUTPUT = CONVERT(@SM, " ", OUTPUT)
      RETURN
      *
      ******************* [ LOAD..DATA ] ******************
      *
LOAD..DATA:
      IF INPUT = "[FI]" THEN
         WRITE IN.REC ON IOFILE, ITEM
         DELETE BP.UPL, "LOADER"
***      OUTPUT = ITEM:" has been written to ":FILEPATH(IOFILE)<3>
         OUTPUT = ITEM:" has been written to ":FILE
      END ELSE
         FILE = FIELD(CONTROL, " ", 1)
         ITEM = FIELD(CONTROL, " ", 2)
         GOSUB OPEN..THE..FILE
         IF ZERROR THEN RETURN
         READ IN.REC FROM IOFILE, ITEM ELSE IN.REC = ""
         IN.REC<-1> = INPUT
         WRITE IN.REC ON IOFILE, ITEM
         OUTPUT = "200-OK"
      END
      RETURN
      *
      **************** [ PREPARE..LOADER ] ****************
      *
PREPARE..LOADER:
      FILE = WORDS<1,2>
      ITEM = WORDS<1,3>
      IF FILE = "" THEN
         OUTPUT = "900-Cannot load to null file!"
         RETURN
      END
      IF ITEM = "" THEN
         OUTPUT = "900-Cannot load to file with null ID!"
         RETURN
      END
      GOSUB OPEN..THE..FILE
      IF ZERROR THEN RETURN
      WRITE "" ON IOFILE, ITEM
      WRITE FILE:" ":ITEM ON BP.UPL, "LOADER"
      OUTPUT = "200-OK"
      RETURN
      *
      **************** [ OPEN..THE..FILE ] ****************
      *
OPEN..THE..FILE:
      TRY  = 1
      LOOP
         IF TRY > 2 THEN 
            ZERROR = true
            OUTPUT = "900-Cannot open/create ":FILE
            RETURN
         END
         OPEN FILE TO IOFILE THEN EXIT
         EXE = "CREATE-FILE ":FILE:" 1,1 7,1 ALU"
         EXECUTE EXE CAPTURING JUNK
         TRY += 1
      REPEAT
      RETURN
   END
