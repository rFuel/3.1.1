$INSERT I_Prologue
      PROMPT ""
      OPEN "CSV" TO SOURCEIO ELSE STOP "Cannot open CSV file."
      READ MAP FROM SOURCEIO, "client.csv" ELSE STOP "Cannot find client.csv in CSV."
      ERR=""
      CALL SR.OPEN.CREATE (ERR, "BOYS", "DYNAMIC", BOYS.DATA)
      IF ERR # "" THEN STOP "Issue creating BOYS file ":ERR
      CALL SR.OPEN.CREATE (ERR, "GIRLS", "DYNAMIC", GRLS.DATA)
      IF ERR # "" THEN STOP "Issue creating GIRLS file ":ERR
      CALL SR.OPEN.CREATE (ERR, "FAMILY", "DYNAMIC", FAMS.DATA)
      IF ERR # "" THEN STOP "Issue creating FAMILY file ":ERR
      CALL SR.OPEN.CREATE (ERR, "COMPANY", "DYNAMIC", COYS.DATA)
      IF ERR # "" THEN STOP "Issue creating COMPANY file ":ERR
      CALL SR.OPEN.CREATE (ERR, "ADDRESS", "DYNAMIC", ADDR.DATA)
      IF ERR # "" THEN STOP "Issue creating ADDRESS file ":ERR
      CALL SR.OPEN.CREATE (ERR, "CLIENT", "DYNAMIC", CLIENT.DATA)
      IF ERR # "" THEN STOP "Issue creating CLIENT file ":ERR
      CALL SR.OPEN.CREATE (ERR, "POST.CODES", "DYNAMIC", PCODES.DATA)
      IF ERR # "" THEN STOP "Issue creating POST.CODES file ":ERR
      CALL SR.FILE.OPEN (ERR, "UPL.CONTROL", UPL.CONTROL)
      IF ERR # "" THEN STOP "Issue with UPL.CONTROL file ":ERR
      PRINT "Starting"
      *
      READ CONTROL.REC FROM UPL.CONTROL, "DBLOAD.PARAMS" ELSE CONTROL.REC = ""
      DATA.SRC    = FIELD(CONTROL.REC<4>, " ", 1)+0
      MAX.CLIENTS = FIELD(CONTROL.REC<1>, " ", 1)+0
      IF MAX.CLIENTS = 0 THEN MAX.CLIENTS = 100
      *
      MONTHS = "JAN.FEB.MAR.APR.MAY.JUN.JUL.AUG.SEP.OCT.NOV.DEC"
      MONTHS = EREPLACE(MONTHS, ".", @FM)
      *
      CLEARFILE CLIENT.DATA
      *
      FRM = 100001
      EOI = FRM + MAX.CLIENTS
      FOR ID = FRM TO EOI
         REC = ""
         A = ID/2
         MALE = (INT(A) = A)
         IF MALE THEN GENDER = "M" ELSE GENDER = "F"
         *
         SURNAME = ""
         COMPANY = ""
         A = ID/33
         IF INT(A) = A THEN
            IS.COMPANY = 1
            LOOP WHILE COMPANY = "" DO
               KEY = RND(250)+1
               READ COMPANY FROM COYS.DATA, KEY ELSE NULL
            REPEAT
            COMPANY = COMPANY<1,1,1>
         END ELSE 
            IS.COMPANY = 0
         END
         LOOP WHILE SURNAME = "" DO
            KEY = RND(3000)
            READ SURNAME FROM FAMS.DATA, KEY ELSE NULL
         REPEAT
         *
         ST.NBR = ""
         STREET = ""
         PCODE  = ""
         SUBURB = ""
         NBR.ENTRIES = RND(4) + 1
         FOR ADRx = 1 TO NBR.ENTRIES
            ST.NBR<-1> = RND(100)+1
            *
            STNAME=""
            LOOP WHILE STNAME = "" DO
               KEY = RND(3000)
               READ STNAME FROM ADDR.DATA, KEY ELSE STNAME = ""
            REPEAT
            STREET<-1> = STNAME
            *
            TMP1 = ""
            TMP2 = ""
            LOOP WHILE TMP1 = "" DO
               KEY = "4":RND(999)
               READ JUNK FROM PCODES.DATA, KEY THEN 
                  TMP1 = KEY
                  EOS  = DCOUNT(JUNK, @VM)
                  TMP2 = JUNK<1,RND(EOS)+1>
               END
            REPEAT
            PCODE<-1>  = TMP1
            SUBURB<-1> = TMP2
         NEXT ADRx
         *
         IF MALE THEN 
            USE.FILE = BOYS.DATA
            TITLE = "MR"
         END ELSE 
            USE.FILE = GRLS.DATA
            TITLE = "MS"
         END
         GIVENS = ""
         NBR.NAMES = 0
         LOOP WHILE NBR.NAMES < 2 DO
            KEY = RND(3000)
            READ NAME FROM USE.FILE, KEY ELSE CONTINUE
            IF GIVENS # "" THEN GIVENS := " "
            GIVENS := NAME
            NBR.NAMES += 1
         REPEAT
         INITIALS = GIVENS[1,1]:FIELD(GIVENS, " ", 2)[1,1]
         *
         DOB = ""
         LOOP WHILE DOB = "" DO
            DD = ("0":RND(31)) "R#2"
            MM = MONTHS<RND(12)+1>
            AGE = RND(65)
            IF AGE < 18 THEN CONTINUE
            DTE = DD:" ":MM:" ":(2018 - AGE)
            IF OCONV(ICONV(DTE, "D4"), "D4") = DTE THEN DOB = ICONV(DTE, "D4")
         REPEAT
         *
         JOINED = ""
         LOOP WHILE JOINED = "" DO
            DD = ("0":RND(31)) "R#2"
            MM = MONTHS<RND(12)+1>
            AGE = RND(50)
            DTE = DD:" ":MM:" ":(2018 - AGE)
            IF OCONV(ICONV(DTE, "D4"), "D4") = DTE THEN JOINED = ICONV(DTE, "D4")
         REPEAT
         *
         LIMIT = ""
         LOOP WHILE LIMIT="" DO
            N = RND(10) * 1000
            IF N > 0 THEN LIMIT = N
         REPEAT
         *
         IF IS.COMPANY THEN
            MBR.TYPE = 12
            SUB.TYPE = MBR.TYPE:RND(5)
         END ELSE
            MBR.TYPE = RND(10)
            SUB.TYPE = MBR.TYPE:RND(5)
         END
         MBR.CLASS = RND(4)+1
         *
         N = RND(4)
         IF N = 0 THEN MARITAL = "S"   ;* single
         IF N = 1 THEN MARITAL = "F"   ;* defactor
         IF N = 2 THEN MARITAL = "M"   ;* married
         IF N = 3 THEN MARITAL = "D"   ;* divorced
         *
         AREA.CODE = "07"
         GOSUB MAKE..PHONE  ; HOME.PHONE = PHONE.NBR
         GOSUB MAKE..PHONE  ; WORK.PHONE = PHONE.NBR 
         AREA.CODE = "04"
         GOSUB MAKE..PHONE  ; MOBL.PHONE = PHONE.NBR 
         *
         ST.NBR = EREPLACE(ST.NBR, @FM, @VM)
         STREET = EREPLACE(STREET, @FM, @VM)
         SUBURB = EREPLACE(SUBURB, @FM, @VM)
         PCODE  = EREPLACE(PCODE , @FM, @VM)
         *
         T1 = ID/119
         SCRAMBLE = (INT(T1) = T1)
         IF SCRAMBLE THEN
            ST.NBR := " ":STREET<1,1,1>:" ":SUBURB<1,1,1>
            STREET = ""
            SUBURB = ""
         END
         CSUM = 1
         LOOP WHILE CSUM < 20 DO
            CSUM = RND(40)
         REPEAT
         CSUM = 256-CSUM
         CHR = ASCII(CSUM)
         CRAZYDATA = STR(CHR, 10)
         *
         REC = ""
         IF IS.COMPANY THEN
            REC<1> = COMPANY
            REC<6> = ""
            REC<7> = GIVENS:" ":SURNAME
            REC<8> = TITLE
            REC<16> = DOB
            REC<17> = GENDER
            REC<18> = ""
         END ELSE
            REC<1> = SURNAME
            REC<6> = INITIALS
            REC<7> = GIVENS
            REC<8> = TITLE
            REC<16> = DOB
            REC<17> = GENDER
            REC<18> = JOINED
         END
         REC<2> = ST.NBR
         REC<3> = STREET
         REC<4> = SUBURB
         REC<5> = PCODE
         REC<18,4> = LIMIT
         REC<21,1,4> = MBR.TYPE
         REC<21,1,5> = SUB.TYPE
         REC<21,1,6> = MBR.CLASS
         REC<22> = DATA.SRC
         REC<25> = MARITAL
         REC<26,1,1> = HOME.PHONE
         REC<26,1,2> = "07"
         REC<26,1,3> = "04":MOBL.PHONE
         REC<26,1,4> = WORK.PHONE
         REC<26,1,5> = "07"
         REC<33> = CRAZYDATA
         REC<38,2,1> = "N"
         REC<38,4,1> = "N"
         REC<38,6,1> = "N"
         REC<38,8,1> = "N"
         WRITE REC ON CLIENT.DATA, ID
         NAP 250
         A = ID/100
         IF INT(A) = A THEN CRT (ID - FRM) "R#4":" records created"
      NEXT ID
*
      PRINT "Done. Created ":MAX.CLIENTS:" cient records"
      STOP
*
MAKE..PHONE:
* ----------
      PHONE.NBR = ""
      FIRST = 1
      JUNK = RND(9)+1
      LOOP WHILE LEN(TRIM(PHONE.NBR)) < 8 DO
         N = RND(10)
         IF N = 0 AND FIRST AND AREA.CODE="07" THEN CONTINUE
         PHONE.NBR := N
         IF LEN(PHONE.NBR) = JUNK THEN PHONE.NBR := " "
         FIRST = 0
      REPEAT
      RETURN
   END
